#
# plot sysbench OLPT summary
#

reset

#output device and size
set terminal png medium nocrop enhanced size 680,320 background '#F0F0F0'

#parameters
set style line 1 lw 2 lc rgbcolor "blue" pt 3
set style line 2 lw 2 lc rgbcolor "green" pt 3
set style line 3 lw 2 lc rgbcolor "magenta" pt 3
set style line 4 lw 2 lc rgbcolor "brown" pt 3

set title 'sysbench OLTP (ro) multi table' font 'giant'
set key at graph 0.85,0.5 box width 2 height 2 font 'small'
set grid

#x-axis
set xtics border
set xlabel 'Threads'

#y-axis
set ylabel 'transactions per second'
set yrange [0:9000]
set ytics border


#
# transactions per second
#

set output "summary-ro-tps.png"

plot \
'summary.ro' index 0 using 4:xtic(3) with linespoints ls 1 title 'MariaDB/InnoDB', \
'' index 2 using 4 with linespoints ls 3 title 'MySQL', \
'' index 3 using 4 with linespoints ls 4 title 'Percona Server',\
'' index 1 using 4 with linespoints ls 2 title 'MariaDB/XtraDB'


set output "summary-rw-tps.png"
set title 'sysbench OLTP (rw) multi table' font 'giant'
set yrange [0:6500]

plot \
'summary.rw' index 0 using 4:xtic(3) with linespoints ls 1 title 'MariaDB/InnoDB', \
'' index 2 using 4 with linespoints ls 3 title 'MySQL', \
'' index 3 using 4 with linespoints ls 4 title 'Percona Server',\
'' index 1 using 4 with linespoints ls 2 title 'MariaDB/XtraDB'


#
# response time
#


set ylabel 'response time [ms]'
set yrange [1:*]
set logscale y 10
set key at graph 0.3,0.8 box width 2 height 2 font 'small'

set output "summary-rw-rt.png"

plot \
'summary.rw' index 0 using 5:xtic(3) with linespoints ls 1 title 'MariaDB/InnoDB', \
'' index 2 using 5 with linespoints ls 3 title 'MySQL', \
'' index 3 using 5 with linespoints ls 4 title 'Percona Server',\
'' index 1 using 5 with linespoints ls 2 title 'MariaDB/XtraDB'


set title 'sysbench OLTP (ro) multi table' font 'giant'
set output "summary-ro-rt.png"

plot \
'summary.ro' index 0 using 5:xtic(3) with linespoints ls 1 title 'MariaDB/InnoDB', \
'' index 2 using 5 with linespoints ls 3 title 'MySQL', \
'' index 3 using 5 with linespoints ls 4 title 'Percona Server',\
'' index 1 using 5 with linespoints ls 2 title 'MariaDB/XtraDB'


